apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'eclipse'

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

repositories {
  jcenter()
  mavenCentral()
}

ext {
  variants = [2.0, 2.3, 2.4]
  variant = System.getProperty("variant") as BigDecimal ?: variants.first()

  javaVersions = [1.6, 1.7, 1.8]
  javaVersion = System.getProperty("java.specification.version") as BigDecimal

  snapshotVersion = true
}

sourceSets.all { ss ->
  for (jv in javaVersions.findAll { it <= javaVersion } ) {
    java {
      srcDir "src/${ss.name}.java$jv/java"
      println "#### added src/${ss.name}.java$jv/java"
    }
    groovy {
      srcDir "src/${ss.name}.java$jv/groovy"
      println "#### added src/${ss.name}.java$jv/groovy"
    }
  }
}

assemble << {
    println "\n${'#'*60}\nENVIRONMENT\n${'#'*60}"
    System.env.keySet().sort().each { key -> println "$key = ${System.env[key]}" }
    println '#'*60

    println "\n\n${'#'*60}\nSYSTEM\n${'#'*60}"
    System.properties.keySet().sort().each { key -> println "$key = ${System.properties[key]}" }
    println '#'*60
}

if (gradle.startParameter.taskNames == ["appveyorCiBuild"]) {
  gradle.startParameter.taskNames = ["build"]
}

if (gradle.startParameter.taskNames == ["travisCiBuild"]) {
  gradle.startParameter.taskNames = ["build"]
  subprojects {
    tasks.withType(Test) {
      maxParallelForks = 2
    }
  }
  if (System.getenv("TRAVIS_PULL_REQUEST") == "false") {
    if (javaVersion == javaVersions.min()) {
      gradle.startParameter.taskNames += ["uploadArchives"]
    }
    if (javaVersion == javaVersions.max()) {
      if (variant == variants.max()) {
        gradle.startParameter.taskNames += ["publishJavadoc", "publishDocs"]
        if (!snapshotVersion) {
          gradle.startParameter.taskNames += ["tagRelease"]
        }
      }
    }
  }
}
